table _Measures
	lineageTag: 72ecb6f6-f0b0-4971-a814-2eecd5a998d6

	measure SalesAmountTotalLCY = ```
			
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY])
			
			
			```
		formatString: #,0
		displayFolder: Sales
		lineageTag: 599da7a2-3b00-44c0-8a80-ce5207150251

		annotation PBI_FormatHint = {"isDecimal":true}

	measure SalesAmount_MTD = ```
			
			TOTALMTD(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    'Calendar'[Date]
			)
			
			```
		displayFolder: Sales
		lineageTag: 3a7937b3-a28e-4cae-ae2e-0c6719ad4235

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmount_YTD = ```
			
			TOTALYTD(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    'Calendar'[Date]
			)
			
			```
		formatString: #,0.00
		displayFolder: Sales
		lineageTag: 9a497722-9df0-439d-9b8a-b28294c6e4bf

	measure SalesAmount_MTD_PreviousYear = ```
			
			CALCULATE(
			    TOTALMTD(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        'Calendar'[Date]
			    ),
			    SAMEPERIODLASTYEAR('Calendar'[Date])
			)
			
			```
		displayFolder: Sales
		lineageTag: 5d49baa1-d5ee-4135-9d2d-9c02fb5310f0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmount_QTD_PreviousYear = ```
			
			CALCULATE(
			    TOTALQTD(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        'Calendar'[Date]
			    ),
			    SAMEPERIODLASTYEAR('Calendar'[Date])
			)
			
			```
		displayFolder: Sales
		lineageTag: c4cf06a7-143d-4e68-b186-aad8bda82cff

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmountPreviousQuarter =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    PREVIOUSQUARTER('Calendar'[Date])
			)
		displayFolder: Sales
		lineageTag: 9bf1d453-a8ae-44a3-9957-28fd96dde64d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmountPreviousMonth =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    PREVIOUSMONTH('Calendar'[Date])
			)
		displayFolder: Sales
		lineageTag: 809a973b-1b81-4cdc-b283-05dec55057fb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmountPreviousYear =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    PREVIOUSYEAR('Calendar'[Date])
			)
		displayFolder: Sales
		lineageTag: fc5a64fd-7988-46ce-b803-ff54203f5f50

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmount_YTD_PreviousYear = ```
			
			CALCULATE(
			    TOTALYTD(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        'Calendar'[Date]
			    ),
			    SAMEPERIODLASTYEAR('Calendar'[Date])
			)
			
			```
		displayFolder: Sales
		lineageTag: a5377c1b-80ad-4971-963f-bae662b4908c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmountYoY% = ```
			
			VAR SalesAmountCurrentYear =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY])
			    )
			    
			VAR SalesAmountPreviousYear =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        SAMEPERIODLASTYEAR('Calendar'[Date])
			    )
			RETURN
			
			DIVIDE(
			    SalesAmountCurrentYear - SalesAmountPreviousYear,
			    SalesAmountPreviousYear
			    )
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Sales
		lineageTag: f4ed915e-904b-4925-ad2d-e5050422862a

	measure SalesAmount_QTD = ```
			
			TOTALQTD(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    'Calendar'[Date]
			)
			
			```
		displayFolder: Sales
		lineageTag: b66d65d2-3765-4bda-bc45-89d04bf02254

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmount_MTD_Today =
			
			
			--Measure for card visualization without date contects
			
			CALCULATE(
			    TOTALMTD(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        'Calendar'[Date]
			    ),
			    'Calendar'[Date] = TODAY()
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 4647d231-6143-4b08-97c1-6d48b3d24372

		annotation PBI_FormatHint = {"isDecimal":true}

	measure SalesAmount_QTD_Today =
			
			
			--Measure for card visualization without date contects
			
			CALCULATE(
			    TOTALMTD(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        'Calendar'[Date]
			    ),
			    'Calendar'[Date] = TODAY()
			)
		displayFolder: Sales
		lineageTag: 7fba4dac-2eec-4b53-8274-028e33791ee8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmountYTDTodayLCY =
			
			
			--Measure for card visualization without date contects
			
			CALCULATE(
			    TOTALYTD(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        'Calendar'[Date]
			    ),
			    'Calendar'[Date] = TODAY()
			)
		displayFolder: Sales
		lineageTag: ae3d4b1c-fa27-4d49-9b25-3ca16a0fc50d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure DistinctInvoiceCount =
			
			DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice])
		formatString: #,0
		displayFolder: Sales
		lineageTag: 18535411-e1ec-4de2-b5ec-8a882fbd8aff

	measure MonthsWithPurchaseLast12M = ```
			
			VAR MaxDate = TODAY()
			VAR StartDate = EDATE(TODAY(), -11)
			
			RETURN
			CALCULATE(
			    DISTINCTCOUNT('Calendar'[YearMonth]),
			    FILTER(
			        'Calendar',
			        'Calendar'[Date] >= StartDate &&
			        'Calendar'[Date] <= MaxDate &&
			        CALCULATE(COUNTROWS('gold v_bc_posted_sales_invoices')) > 0
			    )
			)
			
			```
		formatString: 0
		displayFolder: Customers
		lineageTag: e9735530-1ea9-4312-a18a-e9a9c3dac418

	measure SalesAmountLast12MonthsLCYToday =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    DATESINPERIOD(
			        'Calendar'[Date],
			        TODAY(),         -- punkt końcowy (dzisiejsza data)
			        -12,             -- przesunięcie o 12 miesięcy wstecz
			        MONTH            -- jednostka czasu: miesiąc
			    )
			)
		displayFolder: Sales
		lineageTag: 3a9627e9-d7eb-4d9a-a863-5feb388918f3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure RemainingAmountLCY =
			
			SUMX(
			    VALUES('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    CALCULATE(
			            MAX('gold v_bc_posted_sales_invoices'[RemainingAmountLCY]),
			            ALL('Calendar')
			    )
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 93bdba25-a1dd-46c2-8833-c5be93e32365

		annotation PBI_FormatHint = {"isDecimal":true}

	measure SalesAmountTotal = ```
			
			    SUM('gold v_bc_posted_sales_invoices'[Amount])
			
			
			```
		displayFolder: Sales
		lineageTag: d8fb7aeb-b399-42df-9c7d-d1f76e3aefb6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SalesAmountTotalVAT = ```
			
			    SUM('gold v_bc_posted_sales_invoices'[AmountIncludingVAT])
			
			
			```
		displayFolder: Sales
		lineageTag: 35db0975-9988-4a86-aedc-f78da413d5fd

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure OverdueAmountLCYTotal =
			
			CALCULATE(
			    SUMX(
			        FILTER(
			            SUMMARIZE(
			                ALL('gold v_bc_posted_sales_invoices'),
			                'gold v_bc_posted_sales_invoices'[KeyNoInvoice],
			                "DueDate",   MAX('gold v_bc_posted_sales_invoices'[DueDate]),
			                "RemAmount", MAX('gold v_bc_posted_sales_invoices'[RemainingAmountLCY])
			            ),
			            [DueDate] < TODAY()
			        ),
			        [RemAmount]
			    ),
			    all(),
			    KEEPFILTERS(VALUES('gold v_bc_salesperson'[Code]))
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 897ab103-5690-48ce-9654-2966d8f0d32f

		annotation PBI_FormatHint = {"isDecimal":true}

	measure OverdueInvoicesCount =
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    'gold v_bc_posted_sales_invoices'[DueDate] < TODAY(),
			    'gold v_bc_posted_sales_invoices'[RemainingAmountLCY] > 0
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: b3e5ba7f-ffdb-49f4-ae4c-f900e915efc1

	measure PaidInvoicesCount =
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    'gold v_bc_posted_sales_invoices'[RemainingAmountLCY] = 0
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: acd343fe-4fd1-423a-afb8-7f017609c7b8

	measure RemainingInvoicesCount =
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    'gold v_bc_posted_sales_invoices'[DueDate] > TODAY(),
			    'gold v_bc_posted_sales_invoices'[RemainingAmount] > 0
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 692d79f1-93aa-4e66-a77b-bd383b3e1893

	measure RemainingAmountLCYTotal = ```
			
			CALCULATE(
			    SUMX(
			        VALUES('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			        CALCULATE(MAX('gold v_bc_posted_sales_invoices'[RemainingAmountLCY])                
			        )
			    ),
			    ALL(),
			    KEEPFILTERS(VALUES('gold v_bc_salesperson'[Code]))
			    
			)
			```
		formatString: #,0
		displayFolder: Sales
		lineageTag: ead1fca9-52c9-453b-a551-88f000597f00

		annotation PBI_FormatHint = {"isDecimal":true}

	measure SalesAmountTotalVATLCY = ```
			
			    SUM('gold v_bc_posted_sales_invoices'[AmountIncludingVatLCY])
			
			
			```
		formatString: #,0
		displayFolder: Sales
		lineageTag: 6e353139-2aec-4ea0-a266-91c8a4ed02cf

		annotation PBI_FormatHint = {"isDecimal":true}

	measure LoggedSalesperson =
			
			USERPRINCIPALNAME()
		displayFolder: SalesPerson
		lineageTag: a7b19def-5ccd-43af-94f9-25007ee5f667

	measure Username =
			
			LEFT(
			    USERPRINCIPALNAME(),
			    SEARCH("@",USERPRINCIPALNAME()
			    )-1
			)
		displayFolder: SalesPerson
		lineageTag: f2702a59-34d7-45a1-991e-b2e47911daec

	measure DistinctInvoiceCountCurrentYear = ```
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    YEAR('gold v_bc_posted_sales_invoices'[PostingDate]) = YEAR(TODAY())
			)
			
			```
		formatString: 0
		displayFolder: Sales
		lineageTag: 8a5e62de-97ba-4e4a-92f4-d5e5c901ce83

	measure DistinctInvoiceCountPreviousYear = ```
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    YEAR('gold v_bc_posted_sales_invoices'[PostingDate]) = YEAR(TODAY())-1
			)
			
			```
		formatString: 0
		displayFolder: Sales
		lineageTag: e5f9901d-13fd-4bb4-b506-05058343a4ac

	measure AverageDiscount =
			
			DIVIDE(
			    SUM('gold v_bc_posted_sales_invoices'[LineDiscountAmount]),
			    sum('gold v_bc_posted_sales_invoices'[LinePriceLCY]),
			    0
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Sales
		lineageTag: 141d42d5-af76-47fc-98e3-ff415f40e750

	measure AverageInvoiceAmount = ```
			
			var DistinctInvoiceCount = 
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice])
			RETURN
			
			DIVIDE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    DistinctInvoiceCount
			    )
			```
		formatString: #,0
		displayFolder: Sales
		lineageTag: e6064983-f904-4dfc-833e-3441b2fa1dd5

		annotation PBI_FormatHint = {"isDecimal":true}

	measure CategorySalesAmountLCYLast12MonthsToday =
			
			// VAR SalesAmounthLast12mounths =
			//     CALCULATE(
			//         SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			//         DATESINPERIOD(
			//             'Calendar'[Date],
			//             TODAY(),         -- punkt końcowy (dzisiejsza data)
			//             -12,             -- przesunięcie o 12 miesięcy wstecz
			//             MONTH            -- jednostka czasu: miesiąc
			//         )
			//     )
			
			// RETURN
			
			// SWITCH(
			//     TRUE(),
			//     SalesAmounthLast12mounths <= 10000, "0–10k PLN",
			//     SalesAmounthLast12mounths <= 100000, "10–100k PLN",
			//     SalesAmounthLast12mounths <= 500000, "100–500k PLN",
			//     SalesAmounthLast12mounths > 500000, ">500k PLN",
			//     "Brak sprzedaży"
			// )
			
			VAR SelectedDate =
			    IF(
			        HASONEVALUE('Calendar'[Date]),
			        MAX('Calendar'[Date]),
			        TODAY()
			    )
			
			VAR SalesAmountLast12Months =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        DATESINPERIOD(
			            'Calendar'[Date],
			            SelectedDate,
			            -12,
			            MONTH
			        )
			    )
			
			RETURN
			SWITCH(
			    TRUE(),
			    ISBLANK(SalesAmountLast12Months), "Brak sprzedaży",
			    SalesAmountLast12Months <= 10000, "0–10k PLN",
			    SalesAmountLast12Months <= 100000, "10–100k PLN",
			    SalesAmountLast12Months <= 500000, "100–500k PLN",
			    SalesAmountLast12Months > 500000, ">500k PLN"
			)
		displayFolder: Customers
		lineageTag: bb1a8f7e-9aa4-457c-b557-9f9aba326bd3

	measure SalesAmountLast12Months =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    DATESINPERIOD(
			        'Calendar'[Date],
			        MAX('Calendar'[Date]),  -- używa daty z kontekstu, np. z wykresu
			        -12,
			        MONTH
			    )
			)
		formatString: #,0.00
		displayFolder: Sales
		lineageTag: d33e8022-0444-47af-8340-805b0863740c

	measure DistinctInvoiceCountCurrentMonth =
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    FILTER(
			        ALL('Calendar'),
			        AND(Calendar[Year] = YEAR(TODAY()),
			        Calendar[MonthNumber] = MONTH(TODAY())
			        )
			    )
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 6100c8b1-1f19-4ae8-bf28-6c06ed2ed057

	measure CurrentYearMonthFromCalendar =
			
			CALCULATE(
			    MAX('Calendar'[YearMonth]),
			    FILTER(
			        ALL('Calendar'),
			        AND('Calendar'[Year] = YEAR(TODAY()),
			            'Calendar'[MonthNumber] = MONTH(TODAY())
			        )
			    )
			)
		lineageTag: 32f96e25-2c84-4f3e-b041-c458fa0c5a74

	measure AverageInvoiceAmountCurrentMonth = ```
			
			VAR CurrentYear  = YEAR(TODAY())
			VAR CurrentMonth = MONTH(TODAY())
			VAR SalesAmountCurrentMonth =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = CurrentYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )   
			        )
			    )
			
			VAR DistinctInvoiceCountCurrentMonth =
			    CALCULATE(
			        DISTINCTCOUNT( 'gold v_bc_posted_sales_invoices'[KeyNoInvoice] ),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = CurrentYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )
			        )
			    )
			
			RETURN
			    DIVIDE(SalesAmountCurrentMonth, DistinctInvoiceCountCurrentMonth)
			```
		formatString: #,0
		displayFolder: Sales
		lineageTag: 67f8b3df-2c9a-4bca-b581-bfa111df22be

		annotation PBI_FormatHint = {"isDecimal":true}

	measure AverageDiscountCurrentMonth = ```
			
			VAR CurrentYear  = YEAR(TODAY())
			VAR CurrentMonth = MONTH(TODAY())
			VAR LineDiscountAmountCurrentMonth =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[LineDiscountAmount]),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = CurrentYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )   
			        )
			    )
			
			VAR LinePriceLCY =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[LinePriceLCY]),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = CurrentYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )   
			        )
			    )
			
			RETURN
			    DIVIDE(LineDiscountAmountCurrentMonth, LinePriceLCY)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Sales
		lineageTag: d1ef47b1-962e-4c34-86b5-3a6cf5f5f986

	measure OverdueAmountLCY =
			
			SUMX(
			    FILTER(
			        VALUES('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			        CALCULATE(MIN('gold v_bc_posted_sales_invoices'[DueDate])) < TODAY()
			    ),
			    CALCULATE(MAX('gold v_bc_posted_sales_invoices'[RemainingAmountLCY]))
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 9f01c4f8-93ce-4619-96d4-22ba1056a427

		annotation PBI_FormatHint = {"isDecimal":true}

	measure DniPoTerminie =
			
			Max(
			    'gold v_bc_posted_sales_invoices'[DaysAfterDueDate]
			)
		formatString: 0
		displayFolder: Sales
		lineageTag: a0f3ca58-a221-49a8-a04a-3c617077633a

	measure KolorPoTerminowości = ```
			
			VAR DniPoTerminie = MAX('gold v_bc_posted_sales_invoices'[DaysAfterDueDate])  -- Zamień na nazwę swojej miary lub kolumny
			RETURN 
			    SWITCH(
			        TRUE(),
			        DniPoTerminie > 30, "#E53131",          -- czerwony
			        DniPoTerminie > 14, "#F8BC76",          -- pomarańczowy
			        DniPoTerminie >= 7, "#FFF4AD",          -- żółty
			        BLANK()                                 -- brak koloru
			    )
			```
		displayFolder: Sales
		lineageTag: 2eb404f3-8092-4738-8459-9f80fe8e052d

	measure ItemQuantityCurrentMonth =
			
			CALCULATE(
			    TOTALMTD(
			        SUM('gold v_bc_posted_sales_invoices'[Quantity]),
			        'Calendar'[Date]
			    ),
			    'Calendar'[Date] = TODAY()
			)
		formatString: 0
		displayFolder: Sales
		lineageTag: eaa53807-2939-4c5c-93bb-19c60829ab36

		annotation PBI_FormatHint = {"isDecimal":true}

	measure ItemQuantity =
			
			SUM('gold v_bc_posted_sales_invoices'[Quantity])
		formatString: 0
		displayFolder: Sales
		lineageTag: facf03c2-754e-4a98-afd6-b47e60b90175

		annotation PBI_FormatHint = {"isDecimal":true}

	measure SalesAmountPreviousYearToday =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        PREVIOUSMONTH(
			            DATESINPERIOD(
			                    'Calendar'[Date],
			                    TODAY(),
			                    -1,
			                    MONTH
			            )
			        )
			)
		displayFolder: Sales
		lineageTag: b34bf81e-1b14-40d7-8cdc-8afdb22dfbe0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Sales MTD PY' =
			
			CALCULATE(
			    SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			    DATESMTD(SAMEPERIODLASTYEAR('Calendar'[Date]))
			)
		lineageTag: eed2fded-19ae-4dd1-9e5a-496eee8bc0f4

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure DistinctInvoiceCountCurrentMonthPreviousYear =
			
			CALCULATE(
			    DISTINCTCOUNT('gold v_bc_posted_sales_invoices'[KeyNoInvoice]),
			    FILTER(
			        ALL('Calendar'),
			        AND(Calendar[Year] = YEAR(TODAY())-1,
			        Calendar[MonthNumber] = MONTH(TODAY())
			        )
			    )
			)
		formatString: #,0
		displayFolder: Sales
		lineageTag: 5f72bac1-d3f2-4293-afff-84b85801d34b

	measure AverageInvoiceAmountCurrentMonthPreviousYear = ```
			
			VAR PrevYear  = YEAR(TODAY())-1
			VAR CurrentMonth = MONTH(TODAY())
			VAR SalesAmountCurrentMonth =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[AmountLCY]),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = PrevYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )   
			        )
			    )
			
			VAR DistinctInvoiceCountCurrentMonth =
			    CALCULATE(
			        DISTINCTCOUNT( 'gold v_bc_posted_sales_invoices'[KeyNoInvoice] ),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = PrevYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )
			        )
			    )
			
			RETURN
			    DIVIDE(SalesAmountCurrentMonth, DistinctInvoiceCountCurrentMonth)
			```
		displayFolder: Sales
		lineageTag: 089f6bcb-a8e9-41ff-bc84-396752c8c2de

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure AverageDiscountCurrentMonthPreviousYear = ```
			
			VAR CurrentYear  = YEAR(TODAY())-1
			VAR CurrentMonth = MONTH(TODAY())
			VAR LineDiscountAmountCurrentMonth =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[LineDiscountAmount]),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = CurrentYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )   
			        )
			    )
			
			VAR LinePriceLCY =
			    CALCULATE(
			        SUM('gold v_bc_posted_sales_invoices'[LinePriceLCY]),
			        FILTER(
			            ALL('Calendar'),
			            AND(Calendar[Year] = CurrentYear,
			                Calendar[MonthNumber] = CurrentMonth
			            )   
			        )
			    )
			
			RETURN
			    DIVIDE(LineDiscountAmountCurrentMonth, LinePriceLCY)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Sales
		lineageTag: a062b5df-7add-42cb-927b-917d8efda9f2

	measure PostingDate =
			
			MAX('gold v_bc_posted_sales_invoices'[PostingDate])
		formatString: dd/mm/yyyy
		displayFolder: Sales
		lineageTag: 7c9d4525-3884-45e0-a1df-c4142e60e646

		annotation PBI_FormatHint = {"isDateTimeCustom":true}

	column Value
		isHidden
		formatString: 0
		lineageTag: c4039d76-35c8-4045-9a49-7f19cc97bf58
		summarizeBy: sum
		isNameInferred
		sourceColumn: [Value]

		annotation SummarizationSetBy = Automatic

	partition _Measures = calculated
		mode: import
		source = {0}

	annotation PBI_Id = 95b674d0bd6c45bd8e434895b788fd0a

